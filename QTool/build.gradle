plugins {
    id 'com.android.application'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "cc.hicore.qtool"
        minSdk 26
        targetSdk 26
        def defAppVer = "0.0.7"
        if (System.getenv("KEYSTORE_PATH") != null){
            versionName "${defAppVer}-CI-${System.getenv("CommitNumber")}"
            versionCode System.getenv("CommitNumber")
        }else {
            versionName "${defAppVer}"
            versionCode 999
        }
        multiDexEnabled false
        signingConfigs {
            release{
                if (System.getenv("KEYSTORE_PATH") != null){
                    storeFile = file(System.getenv("KEYSTORE_PATH"))
                    storePassword = System.getenv("KEYSTORE_PASSWORD")
                    keyAlias = System.getenv("KEY_ALIAS")
                    keyPassword = System.getenv("KEY_PASSWORD")
                }
                enableV1Signing = true
                enableV2Signing = true
                enableV3Signing = true
            }
        }
        resConfigs "zh-rCN", "zh"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        additionalParameters '--allow-reserved-package-id','--package-id','0x48'
    }
    applicationVariants.all{ variant ->
        variant.javaCompileProvider.get().doLast{
            println  ("start classes obfuscation "+"${variant.javaCompile.destinationDir}")

            javaexec {
                main = "-jar";
                args = [
                        "../obfuseStringGradle.jar",
                        project.name,
                        variant.javaCompile.destinationDir
                ]
            }
            println  ("obfuse finish")
        }
    }


}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation files('..\\libs\\dx.jar')
    implementation files('..\\libs\\xpopup.aar')
    implementation files('..\\libs\\easy-adapte.aar')

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    annotationProcessor project(':ItemProcesser')
    implementation project(":ItemProcesser")


    implementation 'com.github.kyuubiran:EzXHelper:0.8.8'

    def appCenterSdkVersion = '4.4.3'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation 'com.github.bumptech.glide:glide:4.13.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.1'
    implementation 'com.github.megatronking.stringfog:xor:1.1.0'
}

apply plugin: 'stringfog'

stringfog {
    key 'Hello World'
    enable true
    implementation 'com.github.megatronking.stringfog.xor.StringFogImpl'
    fogPackages = ['cc.hicore']
}